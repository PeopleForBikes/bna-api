set positional-arguments

# Define variables.
top_dir := `git rev-parse --show-toplevel`

# Setup the project.
setup:
    uv sync --all-extras --dev

# Export the OpenAPI specification.
openapi-export:
  aws apigatewayv2 export-api \
    --api-id ivyvugk79e \
    --output-type YAML \
    --specification OAS30 \
    --no-include-extensions \
    openapi.yaml

# Run the integration tests against an environment [localhost, staging].
test *env:
  hurl --test \
    --variables-file $1.vars \
    scenario/pagination.hurl \
    endpoints/cities.hurl \
    endpoints/pricing.hurl \
    endpoints/ratings.hurl \
    endpoints/reports.hurl \
    endpoints/system.hurl \
    endpoints/usstates.hurl

# Run the smoke tests against an environment [localhost, staging].
test-smoke-public *env:
  hurl --test --variables-file $1.vars smoke/public.hurl

# Run the schemathesis tests locally again the openapi schema.
test-localhost-schemathesis:
  RUST_BACKTRACE=1 \
  uv run st run \
    --base-url http://localhost:3000/ \
    --cassette-path cassette.yaml \
    -vvv \
    -x \
    --show-trace \
    --generation-allow-x00 false \
    --exclude-method 'POST' \
    --exclude-method 'PATCH' \
    {{top_dir}}/openapi-3.0.yaml

# Run the schemathesis tests again the openapi schema.
test-staging-schemathesis:
  uv run st run \
    {{top_dir}}/openapi-3.0.yaml \
    --exclude-method 'POST' \
    --exclude-method 'PATCH' \
    --base-url https://api.peopleforbikes.xyz/

# Dump the database with its data (for testing purpose)
dbdump:
  pg_dump $DATABASE_URL -f database-with-test-data.sql
